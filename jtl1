*https://www.tu-ilmenau.de/en/advanced-electromagnetics-group/research/superconductive-high-speed-electronics/rsfq-cell/cells/

.model jjmod jj(Rtype=1, Vg=2.8mV, Cap=0.064pF, R0=200ohm, Rn=16ohm, Icrit=0.1mA)

.subckt jtl 1   5
b1  2   21  jjmod area=2.5
r1  2   21  4.52  *SHUNT=11.30
b2  4   41  jjmod area=2.5
r2  4   41  4.52  *SHUNT=11.30
l1  1   2   2.127p  fcheck
l2  2   3   2.08p   fcheck
l3  3   4   2.08p   fcheck
l4  4   5   2.127p  fcheck
lp1 21  0   0.191p  fcheck
lp2 41  0   0.191p  fcheck
i1  0   3   pwl(0ps 0ma 1ps 350ua)
.ends jtl

X1  jtl 1   5
v2  1   0   pulse(0MV 1MV 0PS 2PS 2PS 1PS 50PS)
R1  5   0   1k

.tran   1p  500p
.file   jtl_out.dat
.print  nodev   1   0
.print  nodev   5   0
